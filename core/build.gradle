dependencies {
    compile "org.apache.commons:commons-lang3:$version_commons_lang3"
    compile "org.springframework.data:spring-data-commons:$version_spring_data"
    compile "org.slf4j:slf4j-simple:$version_slf4j"

    testCompile "junit:junit:$version_junit"
    testCompile "io.github.benas:random-beans:$version_random_beans"
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

archivesBaseName = 'cylone-core'

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}
// 生成 javadoc jar
task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

publishing {
    // 定义发布什么
    publications {
        mavenJava(MavenPublication) {
            groupId = 'net.meku.cylone'
            artifactId = 'cylone-core'
            version = '1.0.0'
            // groupId,artifactId,version，如果不定义，则会按照以上默认值执行
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                // 构件名称
                // 区别于artifactId，可以理解为artifactName
                name = 'Cylone'
                // 构件描述
                description = 'A light-weight library to convert pojos'
                // 构件主页
                url = 'https://github.com/shoopman/cylone'
                // 许可证名称和地址
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // 开发者信息
                developers {
                    developer {
                        name = 'Cary Leung'
                        email = 'shoopman917@gmail.com'
                    }
                }
                // 版本控制仓库地址
                scm {
                    url = 'https://github.com/shoopman/cylone'
                    connection = 'scm:git:git://github.com/shoopman/cylone.git'
                    developerConnection = 'scm:git:ssh://git@github.com:shoopman/cylone.git'
                }
            }
        }
    }
    // 定义发布到哪里
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                // 这里就是之前在issues.sonatype.org注册的账号
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    // <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
    // 防止本地打开中文乱码
    options.addStringOption("charset", "UTF-8")
}
